
Package arden.constants;

Helpers
    ascii = [0..0x007f];

    space = ' ';
    tab = 0x0009;
    lf = 0x000a;
    cr = 0x000d;
    wschar = space | tab | lf | cr;
    
    input = [ascii - [cr + lf]]; 
    
    digit = ['0'..'9'];
    sign = ('+' | '-');
    exp = ('e' | 'E') sign? digit+;
    
    string_char = [input - '"'];
    
    fractional_seconds =
          /* empty */
        | '.' digit+;
        
    time_zone =
          /* empty */
        | 'Z'
        | 'z'
        | '+' digit digit ':' digit digit
        | '-' digit digit ':' digit digit; 
	
	a = ['a' + 'A'];
    b = ['b' + 'B'];
    c = ['c' + 'C'];
    d = ['d' + 'D'];
    e = ['e' + 'E'];
    f = ['f' + 'F'];
    g = ['g' + 'G'];
    h = ['h' + 'H'];
    i = ['i' + 'I'];
    j = ['j' + 'J'];
    k = ['k' + 'K'];
    l = ['l' + 'L'];
    m = ['m' + 'M'];
    n = ['n' + 'N'];
    o = ['o' + 'O'];
    p = ['p' + 'P'];
    q = ['q' + 'Q'];
    r = ['r' + 'R'];
    s = ['s' + 'S'];
    t = ['t' + 'T'];
    u = ['u' + 'U'];
    v = ['v' + 'V'];
    w = ['w' + 'W'];
    x = ['x' + 'X'];
    y = ['y' + 'Y'];
    z = ['z' + 'Z'];
    
Tokens
    comma = ',';
    l_par = '(';
    r_par = ')';
    null = n u l l;
	true = t r u e;
	false = f a l s e;
    
    arden_number =  
          sign? (digit+ ('.' digit*)? exp?) 
        | sign? ('.' digit+ exp?);

    arden_string = 
        '"' ( 
            string_char* ('"' '"' string_char*)* 
        ) '"';
        
    arden_date = digit digit digit digit '-' digit digit '-' digit digit;
    
    arden_date_time = digit digit digit digit '-' digit digit '-' digit digit t digit digit ':' digit digit ':' digit digit fractional_seconds time_zone;        
        
    ws = wschar+;
    
    year  = y e a r s?;
    month = m o n t h s?;
    week = w e e k s?;
    day = d a y s?;
    hour = h o u r s?;
    minute = m i n u t e s?;
    second = s e c o n d s?;
    

Ignored Tokens
    ws;

Productions
    arden_constant = expr;
    
    duration_op =
          {year} year
        | {month} month
        | {week} week
        | {day} day
        | {hour} hour
        | {min} minute
        | {sec} second;
    
    arden_boolean = 
		  {true} true 
		| {false} false;
	
    expr = 
          {atom} atom
        | {listatom} comma atom
        | {list} expr comma atom;
    
    atom = 
          {number} arden_number 
        | {string} arden_string 
        | {boolean} arden_boolean
        | {date} arden_date
        | {dtime} arden_date_time
        | {duration} arden_number duration_op
        | {null} null
        | {expr} l_par expr r_par
        | {par} l_par r_par;